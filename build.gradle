apply plugin: 'java'

repositories {
  mavenCentral()
}

configurations {
  antlr4
}

dependencies {
  compile 'org.antlr:antlr4-runtime:4.5'
  antlr4 'org.antlr:antlr4:4.5'
}

ext.antlr4GeneratedSourceDir = new File(buildDir, 'generated-sources/antlr4')

sourceSets {
  main {
    java {
      srcDir antlr4GeneratedSourceDir
    }
  }
}

task generateGrammarSource {
  ext.grammarFile = new File(projectDir, 'src/main/antlr4/Java.g4')
  ext.outputPackage = 'com.lemoulinstudio.sdiff.java.tree.parser'
  ext.outputDir = new File(antlr4GeneratedSourceDir, outputPackage.replace('.' as char, File.separatorChar))
  
  inputs.file grammarFile
  inputs.property 'outputPackage', outputPackage
  outputs.dir outputDir
  
  doLast {
    javaexec {
      classpath configurations.antlr4
      main 'org.antlr.v4.Tool'
      args '-o', outputDir,
           '-package', outputPackage,
           grammarFile
    }
  }
}

compileJava.dependsOn(generateGrammarSource)

ext.mainClass = 'com.lemoulinstudio.sdiff.Main'
